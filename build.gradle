apply plugin: 'maven'
apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'signing'

group = 'com.github.ci-cd'
version = '1.0'

defaultTasks 'install'

idea {
	module {
		downloadJavadoc = true
		downloadSources = true
	}
}

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

ext {
	libraries = [
		// Compile dependencies...
		'commons-lang3':[
			'org.apache.commons:commons-lang3:3.1'
		],
		'httpclient':[
			'org.apache.httpcomponents:httpclient:4.2.2'
		],
		'mustache':[
			'com.github.spullara.mustache.java:compiler:0.8.12'
		],
		'paranamer':[
			'com.thoughtworks.paranamer:paranamer:2.5.2'
		],
		'swagger-jaxrs':[
			'com.wordnik:swagger-jaxrs_2.10:1.3.2'
		],
		'swagger-jersey-jaxrs':[
			'com.wordnik:swagger-jersey-jaxrs_2.10:1.3.2'
		],
		'reflections':[
			'org.reflections:reflections:0.9.9-RC1'
		],
		'commons-io':[
			'commons-io:commons-io:2.0.1'
		],
		'slf4j':['org.slf4j:slf4j-api:1.7.2'],
		'rs-api':[
			'javax.ws.rs:javax.ws.rs-api:2.0'
		],
		'logback':[
			'ch.qos.logback:logback-classic:1.0.13'
		],
		'junit':['junit:junit:4.11'],
	]
}

repositories {
	mavenLocal()
	maven { url "http://repo.maven.apache.org/maven2" }
}

dependencies {
	compile gradleApi()
	compile localGroovy()

	compile libraries.'commons-lang3'
	compile libraries.'httpclient'
	compile libraries.'mustache'
	compile libraries.'swagger-jaxrs'
	compile(libraries.'swagger-jersey-jaxrs') { exclude group: 'asm' }
	compile libraries.'reflections'
	compile libraries.'commons-io'
	compile libraries.'rs-api'
	compile libraries.'slf4j'
	compile libraries.'logback'

	testCompile libraries.'junit'
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.10'
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

signing { sign configurations.archives }

uploadArchives {
	repositories {
		mavenDeployer {
			beforeDeployment { MavenDeployment deployment ->
				signing.signPom(deployment)
			}

			repository (url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}

			snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}

			pom.project {
				name = project.group + ':' + project.name
				packaging 'jar'
				description 'This gradle plugin is used to generate docs for Rest APIs '+
						'that can be used with swagger-ui. This plugin is equivalent to '+
						'com.github.kongchen:swagger-maven-plugin'
				url 'https://github.com/ci-cd/gradle-swagger-plugin'
				scm {
					connection 'scm:git:git://github.com/ci-cd/gradle-swagger-plugin.git'
					developerConnection 'scm:git:ssh://github.com:ci-cd/gradle-swagger-plugin.git'
					url 'https://github.com/ci-cd/gradle-swagger-plugin'
				}
				licenses {
					license {
						name 'The Apache Software License, Version 2.0'
						url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
						distribution 'repo'
					}
				}
				developers {
					developer {
						name 'Hari Samala'
						email 'hari2139@gmail.com'
					}
				}
			}
		}
	}
}
